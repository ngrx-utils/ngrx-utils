package(default_visibility = ["//visibility:public"])

exports_files([
    "tsconfig.json",
    "tsconfig.spec.json",
    "LICENSE",
])

# Developers should always run `bazel run :install`
# This ensures that package.json in subdirectories get installed as well.
alias(
    name = "install",
    actual = "@yarn//:yarn",
)

alias(
    name = "buildtools",
    actual = "@com_github_bazelbuild_buildtools//buildifier",
)

filegroup(
    name = "node_modules",
    # NB: rxjs is not in this list, because we build it from sources using the
    # label @rxjs//:rxjs
    srcs = glob(["/".join([
        "node_modules",
        pkg,
        "**",
        ext,
    ]) for pkg in [
        "@angular",
        "@angular-devkit",
        "@ngrx",
        "@types",
        "tsickle",
        "tsutils",
        "tslib",
        "jasmine",
        "typescript",
        "zone.js",
        "fast-glob",
    ] for ext in [
        "*.js",
        "*.json",
        "*.d.ts",
    ]] + [
        "node_modules/http-server/**",
    ]),
)

ANGULAR_TESTING = [
    "node_modules/@angular/*/bundles/*-testing.umd.js",
    # We use AOT, so the compiler and the dynamic platform-browser should be
    # visible only in tests
    # "node_modules/@angular/compiler/bundles/*.umd.js",
    # "node_modules/@angular/platform-browser-dynamic/bundles/*.umd.js",
]

# Files necessary for unit tests that use zonejs
filegroup(
    name = "web_test_bootstrap_scripts",
    # The order of these deps is important.
    # Do not sort.
    srcs = [
        "//:node_modules/reflect-metadata/Reflect.js",
        "//:node_modules/zone.js/dist/zone.js",
        "//:node_modules/zone.js/dist/async-test.js",
        "//:node_modules/zone.js/dist/sync-test.js",
        "//:node_modules/zone.js/dist/fake-async-test.js",
        "//:node_modules/zone.js/dist/proxy.js",
        "//:node_modules/zone.js/dist/jasmine-patch.js",
    ],
)

filegroup(
    name = "tslib_bundle",
    testonly = 1,
    srcs = glob(["node_modules/tslib/tslib.js"]),
)

filegroup(
    name = "angular_bundles",
    srcs = glob(
        [
            "node_modules/@angular/*/bundles/*.umd.js",
            # "node_modules/@ngrx/*/bundles/*.umd.js",
        ],
        exclude = ANGULAR_TESTING + [
            "node_modules/@angular/bazel/**/*",
            "node_modules/@angular/cli/**/*",
            "node_modules/@angular/compiler-cli/**/*",
            "node_modules/@angular/language-service/**/*",
            "node_modules/@ngrx/schematics/**/*",
        ],
    ),
)

filegroup(
    name = "angular_test_bundles",
    testonly = 1,
    srcs = glob(ANGULAR_TESTING),
)
